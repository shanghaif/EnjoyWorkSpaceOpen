apply plugin: 'com.android.application'

//配套配置信息
ext {
    //应用名称
    applicationName = "808"
}

android {
    signingConfigs {
        release {
            storeFile file("${rootProject.ext.KeyStoreFllePath}")
            storePassword KeyStorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.kuyou.jt808"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "${versionNumberCustom()}"

        ndk {
            abiFilters 'armeabi-v7a', "arm64-v8a"
        }
    }
    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release //加入签名配置
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${rootProject.ext.projectHardwareName}_${defaultConfig.versionName}.apk"
        }
    }
}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly files('compileOnlyLibs/framework.jar')

    implementation project(path: ':common')
    implementation project(path: ':common_ku09')
    implementation project(path: ':sdk_jt808')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-rc01'
    implementation("com.google.guava:guava:28.2-android")
    implementation 'com.amap.api:location:4.7.0'
    implementation 'com.fanjun:keeplive:1.1.20'
    implementation 'com.noober.light:core:1.1.0'
    implementation("com.squareup.okhttp3:okhttp:4.2.0")
}

//added by wgx
def versionNumberCustom() {
    def buildType = "debug"
    if (isReleaseBuildType()) {
        buildType = "release"
    }
    return "${project.applicationName}_" + "${buildType}_" + rootProject.ext.versionDate;
}

boolean isReleaseBuildType() {
    for (String s : gradle.startParameter.taskNames) {
        if (s.contains("Release") | s.contains("release")) {
            return true
        }
    }
    return false
}
